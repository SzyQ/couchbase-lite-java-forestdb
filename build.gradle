apply plugin: 'com.android.library'
//apply plugin: 'maven'

sourceCompatibility = JavaVersion.VERSION_1_6
targetCompatibility = JavaVersion.VERSION_1_6

buildscript {
    repositories {
        //mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.3.0'
    }
}

repositories {
    jcenter()
}

android {
    compileSdkVersion 23
    buildToolsVersion "22.0.1"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // workaround for "duplicate files during packaging of APK" issue
    // see https://groups.google.com/d/msg/adt-dev/bl5Rc4Szpzg/wC8cylTWuIEJ
    packagingOptions {
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }

    sourceSets {
        main {
            jni.srcDirs = [] // This prevents the auto generation of Android.mk
            jniLibs.srcDir file('libs') // This is not necessary unless you have precompiled libraries in your project.
        }
    }

    // call regular ndk-build(.cmd) script from app directory
    task buildNative(type: Exec, description: 'Compile JNI source via NDK') {
        println(android.ndkDirectory)
        def ndkDir = android.ndkDirectory
        commandLine "$ndkDir/ndk-build", '-C', file('jni').absolutePath
    }

    task cleanNative(type: Exec, description: 'Clean JNI object files') {
        def ndkDir = android.ndkDirectory
        commandLine "$ndkDir/ndk-build", '-C', file('jni').absolutePath, 'clean'
    }

    clean.dependsOn 'cleanNative'

    // Define a task for ndk-build
    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn buildNative
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:23.0.0'
    androidTestCompile 'com.fasterxml.jackson.core:jackson-databind:2.6.0'
    androidTestCompile 'com.fasterxml.jackson.core:jackson-core:2.6.0'
    androidTestCompile 'com.fasterxml.jackson.core:jackson-annotations:2.6.0'
}

